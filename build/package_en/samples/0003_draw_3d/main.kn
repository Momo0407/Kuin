; Draw 3D Sample (C)Kuina-chan

func main()
	var wndMain: wnd@Wnd :: wnd@makeWnd(null, %aspect, 1600, 900, "Draw 3D Sample") {Creating a window.}
	var drawMain: wnd@Draw :: wnd@makeDraw(wndMain, 0, 0, 1600, 900, %scale, %scale, false) {Creating a draw control.}
	var objBoard: draw@Obj :: draw@makeObj("res/board.knobj") {Loading the board model.}
	var texBoard: draw@Tex :: draw@makeTex("res/board.png") {Loading the texture for the board model.}
	var objBox: draw@Obj :: draw@makeObj("res/box.knobj") {Loading the box model.}
	var texBox: draw@Tex :: draw@makeTex("res/box.png") {Loading the texture for the box model.}
	var objSphere: draw@Obj :: draw@makeObj("res/sphere.knobj") {Loading the sphere model.}
	var texSphere: draw@Tex :: draw@makeTex("res/sphere.png") {Loading the texture for the sphere model.}
	var objCone: draw@Obj :: draw@makeObj("res/cone.knobj") {Loading the cone model.}
	var texCone: draw@Tex :: draw@makeTexEvenArgb(1.0, 0.35, 0.1, 0.05)
	var texConeSpecular: draw@Tex :: draw@makeTexEvenArgb(2.0, 0.3, 0.3, 0.3)
	do draw@clearColor(0xFF999999) {Setting the background color.}
	do draw@depth(true, true) {Setting the Z buffer.}
	do objBoard.pos(20.0, 20.0, 20.0, 0.0, 0.0, 0.0, 0.0, 0.0, -15.0) {Set the position of the board model.}
	do objBox.pos(1.0, 1.0, 1.0, 0.0, 0.0, 0.0, 0.0, 0.5, 0.0) {Set the position of the box model.}
	do objSphere.pos(1.0, 1.0, 1.0, 0.0, 0.0, 0.0, 2.0, 0.5, 0.0) {Set the position of the sphere model.}
	do objCone.pos(2.0, 2.0, 2.0, 0.0, 0.0, 0.0, -2.0, 0.0, 0.0) {Set the position of the cone model.}
	var angle: float :: 0.0
	while(wnd@act())
		do angle :+ lib@pi / 120.0 {Make it go around once every 4 seconds.}
		do draw@camera(15.0 * lib@sin(angle), 4.0, 15.0 * lib@cos(angle), 0.0, 0.0, 0.0, 0.0, 1.0, 0.0) {Setting up the camera.}
		do objBoard.draw(0, 0.0, texBoard, null, null) {Drawing the board model.}
		do objBox.draw(0, 0.0, texBox, null, null) {Drawing the box model.}
		do objSphere.draw(0, 0.0, texSphere, null, null) {Drawing the sphere model.}
		do objCone.draw(0, 0.0, texCone, texConeSpecular, null) {Drawing the cone model.}
		do draw@render(60)
	end while
end func
