; Sound Sample (C)Kuina-chan

var sndSe: snd@Snd
var sndBgm: snd@Snd

func main()
	do ogg@init() {Initializing the Ogg library.}
	var wndMain: wnd@Wnd :: wnd@makeWnd(null, %normal, 1024, 768, "Sound Sample")
	var btnSe: wnd@Btn :: wnd@makeBtn(wndMain, 12, 12, 150, 23, %fix, %fix, "Play SE") {Creating the SE button.}
	do btnSe.onPush :: btnSeOnPush {Event when the SE button is pressed.}
	var btnBgm: wnd@Btn :: wnd@makeBtn(wndMain, 168, 12, 150, 23, %fix, %fix, "Play BGM") {Creating the BGM button.}
	do btnBgm.onPush :: btnBgmOnPush {Event when the BGM button is pressed.}
	do @sndSe :: snd@makeSnd("res/se.wav") {Load sound as non-streaming.}
	do @sndBgm :: snd@makeSnd("res/bgm.ogg") {Load sound as streaming.}
	while(wnd@act())
	end while
	
	func btnSeOnPush(wnd: wnd@WndBase)
		do @sndSe.setPos(0.0) {Set the playback position to 0 seconds.}
		do @sndSe.play() {Play SE when the SE button is pressed.}
	end func
	
	func btnBgmOnPush(wnd: wnd@WndBase)
		var btn: wnd@Btn :: wnd $ wnd@Btn
		if(@sndBgm.playing())
			do @sndBgm.stop() {When the BGM button is pressed, stop BGM if it is playing.}
			do btn.setText("Play BGM")
		else
			do @sndBgm.playLoop() {When the BGM button is pressed, play BGM in a loop if it is stopped.}
			do btn.setText("Stop BGM")
		end if
	end func
end func
