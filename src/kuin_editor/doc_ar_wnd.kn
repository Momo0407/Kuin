+class DocArWnd(\doc_ar@DocAr)
	*func ctor()
		do super(me)
		do me.root :: #@ObjWnd
		var width: int
		var height: int
		do me.root.getDefaultSize(&width, &height)
		do me.root.init("wndMain", true, 0, 0, width, height)
	end func

	+*func fix()
		var lines: list<[]char> :: #list<[]char>
		do lines.add("var \{me.root.name}: \{(me.root $ @ObjArWnd).type()}")
		do me.nodes.head()
		while(!me.nodes.term())
			var node: @ObjArWnd :: me.nodes.get() $ @ObjArWnd
			do lines.add("var \{node.name}: \{node.type()}")
			do me.nodes.next()
		end while
		do lines.add("")
		do lines.add("+func make(): wnd@Wnd")
		do me.root.getSrc(1, lines)
		do me.nodes.head()
		while(!me.nodes.term())
			do me.nodes.get().getSrc(1, lines)
			do me.nodes.next()
		end while
		do lines.add("\tret @\{me.root.name}")
		do lines.add("end func")
		do me.src :: lines.toArray()
	end func

	*func makeObj(name: []char): \doc_ar@Obj
		switch(name)
		case "Wnd"
			ret #@ObjWnd
		case "Btn"
			ret #@ObjBtn
		end switch
		ret null
	end func

	*func updateLt()
		do \form@listLt.add("Btn", -1)
	end func
end class

class ObjArWnd(\doc_ar@Obj)
	+func type(): []char
	end func
end class

class ObjWnd(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float)
		do draw@rect(x, y, width, height, 0xFFF0F0F0)
		do draw@rect(x, y - 30.0, width, 30.0, 0xFFFFFFFF)
		do draw@rectLine(x - 1.0, y - 31.0, width + 2.0, height + 32.0, 0xFF1883D7)
		do \common@fontP.draw(x + 30.0, y - 30.0 + 6.0, me.props.get("text", &), draw@black)
	end func

	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 50
		do minHeight :: 50
		do maxWidth :: lib@intMax
		do maxHeight :: lib@intMax
	end func

	+*func getDefaultSize(width: &int, height: &int)
		do width :: 640
		do height :: 480
	end func

	+*func getSrc(indent: int, lines: list<[]char>)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeWnd(null, %normal, \{me.width}, \{me.height}, \"\{me.props.get("text", &)}\")")
	end func

	+*func kind(): []char
		ret "Wnd"
	end func

	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int)
		do super(me, name, visible, x, y, width, height)
		do me.props.add("text", me.name)
	end func

	+*func type(): []char
		ret "wnd@Wnd"
	end func

	*func propsNames(): [][]char
		ret ["text"]
	end func
end class

class ObjBtn(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float)
		do draw@rect(x, y, width, height, 0xFFE1E1E1)
		do draw@rectLine(x, y, width, height, 0xFFADADAD)
		var text: []char :: me.props.get("text", &)
		do \common@fontP.draw(lib@floor(x + (width - \common@fontP.calcWidth(text)) / 2.0), lib@floor(y + (height - \common@fontP.maxHeight()) / 2.0), text, draw@black)
	end func

	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 10
		do minHeight :: 23
		do maxWidth :: lib@intMax
		do maxHeight :: lib@intMax
	end func

	+*func getDefaultSize(width: &int, height: &int)
		do width :: 75
		do height :: 23
	end func

	+*func getSrc(indent: int, lines: list<[]char>)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeBtn(null, \{me.x}, \{me.y}, \{me.width}, \{me.height}, %fix, %fix, \"\{me.props.get("text", &)}\")")
	end func

	+*func kind(): []char
		ret "Btn"
	end func

	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int)
		do super(me, name, visible, x, y, width, height)
		do me.props.add("text", me.name)
	end func

	+*func type(): []char
		ret "wnd@Btn"
	end func

	*func propsNames(): [][]char
		ret ["text"]
	end func
end class
