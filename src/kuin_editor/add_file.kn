var wndAddFile: wnd@Wnd
var wndAddFileResuiltFile: []char
var wndAddFileResuiltType: int
var wndAddFileEditFile: wnd@Edit
var wndAddFileListType: wnd@List

+func addNewFile()
	do @wndAddFile :: wnd@makeWnd(\form@wndMain, (%fix $ wnd@WndStyle).or(%noMinimize), 640, 480, \common@langEn?("Add New File", "新しいファイルを追加"))
	do @wndAddFileResuiltFile :: null
	do @wndAddFileResuiltType :: 0
	do @wndAddFileListType :: wnd@makeList(@wndAddFile, 12, 12, 616, 100, %fix, %scale)
	do @wndAddFileListType.add(\common@langEn?("Source Code", "ソースコード"))
	do @wndAddFileListType.add(\common@langEn?("Window", "ウインドウ"))
	do @wndAddFileListType.setSel(0)
	do @wndAddFileEditFile :: wnd@makeEdit(@wndAddFile, 12, 118, 616, 19, %fix, %fix)
	var btnAdd: wnd@Btn :: wnd@makeBtn(@wndAddFile, 12, 143, 100, 23, %fix, %fix, \common@langEn ?("Add", "追加"))
	do btnAdd.onPush :: btnAddOnPush
	do @wndAddFile.modal()
	if(@wndAddFileResuiltFile <>& null)
		var doc: \doc@Doc :: null
		switch(@wndAddFileResuiltType)
		case 0
			do doc :: #\doc_src@DocSrc
		case 1
			do doc :: #\doc_gen@DocGen
			do (doc $ \doc_gen@DocGen).create(#\doc_ar_wnd@DocArWnd)
		end switch
		if(doc <>& null)
			do doc.changed :: true
			do \src@docs.add(@wndAddFileResuiltFile, doc)
			do \src@setCurSrc(doc)
			do \prop@changeProp()
		end if
	end if
	do @wndAddFile :: null
	do @wndAddFileResuiltFile :: null
	do @wndAddFileEditFile :: null
	do @wndAddFileListType :: null

	func btnAddOnPush(wnd: wnd@WndBase)
		do @wndAddFileResuiltFile :: @wndAddFileEditFile.getText().replace("\\", "/").trim()
		do @wndAddFileResuiltType :: @wndAddFileListType.getSel()
		if(valid(@wndAddFileResuiltFile))
			var ext: []char :: null
			switch(@wndAddFileResuiltType)
			case 0
				do ext :: ".kn"
			case 1
				do ext :: ".kngen"
			default
				do ext :: ""
			end switch
			do @wndAddFileResuiltFile :~ ext
			if(!\src@docs.exist(@wndAddFileResuiltFile))
				do @wndAddFile.close()
			else
				do wnd@msgBox(\form@wndMain, \common@langEn ?("The file name is already existed.", "そのファイル名は既に存在します。"), \common@title, %err, %ok)
			end if
		else
			do wnd@msgBox(\form@wndMain, \common@langEn ?("The file name is invalid.", "ファイル名が不正です。"), \common@title, %err, %ok)
		end if

		func valid(name: []char): bool
			if(^name = 0)
				ret false
			end if
			if(!('a' <= name[0] & name[0] <= 'z' | name[0] = '_'))
				ret false
			end if
			for i(1, ^name - 1)
				if(!('a' <= name[i] & name[i] <= 'z' | '0' <= name[i] & name[i] <= '9' | name[i] = '_' | name[i] = '.' | name[i] = '/'))
					ret false
				end if
			end for
			ret true
		end func
	end func
end func

+func addExistingFile()
	if(\src@mainSrcDir = "")
		do wnd@msgBox(\form@wndMain, \common@langEn ?("The main source file must be saved before adding another existed file.", "既存のファイルを追加する前にメインソースファイルを保存しなければなりません。"), \common@title, %err, %ok)
		ret
	end if
	var file: []char :: wnd@openFileDialog(\form@wndMain, [\common@langEn ?("Kuin supported file (*.kn,*.kngen)", "Kuinがサポートするファイル (*.kn,*.kngen)"), "*.kn;*.kngen"], 0)
	if(file <>& null)
		var doc: \doc@Doc :: \src@makeDoc(file)
		if(doc <>& null & \src@loadFileToDocs(file, doc))
			do \src@setCurSrc(doc)
			do \prop@changeProp()
		else
			var msg: []char
			if(\common@langEn)
				do msg :: "Loading failed. " ~ file
			else
				do msg :: "読み込みに失敗しました。 " ~ file
			end if
			do wnd@msgBox(\form@wndMain, msg, \common@title, %err, %ok)
		end if
	end if
end func
